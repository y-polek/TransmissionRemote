apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 26
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 91104
        versionName '0.9.11.4'
        applicationId 'net.yupol.transmissionremote.app'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [fabricApiKey: "${apiKey('fabric_api_key')}"]
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            lintOptions {
                disable 'MissingTranslation'
                abortOnError false
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    dataBinding {
        enabled true
    }

    useLibrary 'org.apache.http.legacy'
}

dependencies {
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.google.guava:guava:23.6-android'
    compile 'com.google.code.gson:gson:2.8.2'
    compile('com.octo.android.robospice:robospice:1.4.14') {
        exclude (group: 'commons-io', module: 'commons-io')
    }
    compile('com.octo.android.robospice:robospice-google-http-client:1.4.14') {
        exclude (group: 'org.apache.httpcomponents', module: 'httpclient')
        exclude (group: 'commons-io', module: 'commons-io')
    }
    compile('com.google.http-client:google-http-client-jackson2:1.22.0') {
        exclude (group: 'org.apache.httpcomponents', module: 'httpclient')
    }

    compile('com.mikepenz:materialdrawer:6.0.8@aar') {
        transitive = true
    }
    compile 'com.mikepenz:iconics-core:3.0.4@aar'
    compile "com.mikepenz:iconics-views:3.0.4@aar"
    compile 'com.mikepenz:google-material-typeface:3.0.1.3.original@aar'
    compile 'com.mikepenz:fontawesome-typeface:5.3.1.1@aar'
    compile 'com.mikepenz:community-material-typeface:2.7.94.1@aar'
    compile 'com.mikepenz:ionicons-typeface:2.0.1.5@aar'

    compile 'com.github.sevar83:indeterminate-checkbox:1.0.5@aar'

    compile('com.crashlytics.sdk.android:crashlytics:2.9.7@aar') {
        transitive = true
    }

    compile 'com.github.hotchemi:permissionsdispatcher:2.2.0'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.2.0'

    compile 'com.getbase:floatingactionbutton:1.10.1'

    compile 'com.evernote:android-job:1.2.2'

    compile 'org.apache.commons:commons-io:1.3.2'

    // Emoji
    implementation 'com.vdurmont:emoji-java:4.0.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'com.android.support:support-annotations:26.1.0'
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test:rules:1.0.1'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'org.mockito:mockito-core:2.13.0'
    androidTestCompile "org.mockito:mockito-android:2.13.0"
}

def property(String fileName, String propertyName) {
    def propsFile = rootProject.file(fileName)
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props[propertyName] != null) {
            return props[propertyName]
        } else {
            throw new GradleException("There is no '" + propertyName + "' property in '" + propsFile.name + "' file")
        }
    } else {
        throw new GradleException("'" + propsFile.name + "' file does not exist")
    }
}

def apiKey(String name) {
    return property("api_keys.properties", name)
}
